#!/usr/bin/env perl

use strict;
use warnings;

use Class::Load ();
use Module::CPANfile;
use JSON;

my %modules;

if (-f 'cpanfile') {
    my $file = Module::CPANfile->load('cpanfile');

    my $prereqs = $file->prereqs->merged_requirements->as_string_hash;

  PREREQ: foreach my $prereq (keys %$prereqs) {
        Class::Load::try_load_class($prereq) or do {
            warn "Can't load '$prereq': $@\n";
            next PREREQ;
        };

        my $version = $prereq->VERSION;

        $modules{$prereq} = $version;
    }
}
else {
    die "Can't detect dependencies\n";
}

print JSON->new->canonical(1)->pretty->encode([ map { { module => $_, version => $modules{$_} } } keys %modules ]);
